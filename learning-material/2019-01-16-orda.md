---
layout: page
title: 教材
permalink: /learning-material/2019-01-16/
standalone: true
---
    
* content
{:toc}

## スクリーンショット

<img width="500" alt="img1" src="https://user-images.githubusercontent.com/10509075/51163651-c5fc5c00-18dd-11e9-94ce-88cb5d147bd8.png">

<img width="500" alt="img2" src="https://user-images.githubusercontent.com/10509075/51164214-92bacc80-18df-11e9-8826-22763aed0df0.png">

## ダウンロード

* <i class="fa fa-download" aria-hidden="true"></i> [はじめる](https://github.com/4D-JP/learning-material/releases/download/2019-01-16/tutorial-start.zip)

* <i class="fa fa-download" aria-hidden="true"></i> [できた](https://github.com/4D-JP/learning-material/releases/download/2019-01-16/tutorial-finish.zip)

## データストアのセットアップ

* テキストファイルの読み込み（``Get 4D folder`` ``Document to text``）

* テーブルの作成（``IMPORT STRUCTURE``）

* データストアの更新（``RESTART 4D``）

* データストアオブジェクト（``ds``）

* コレクション型とオブジェクト型（``C_COLLECTION`` ``C_OBJECT``）

* エンティティの追加（``JSON Parse`` ``dataClass.new()`` ``entity.fromObject()`` ``entity.save()``）

**謝辞**: サンプルデータ by [tm-webtools.com](http://tm-webtools.com/Tools/TestData)

## ユーザーインタフェース

#### リストボックスのプロパティ（１個目）

 * コレクションまたはエンティティセレクション

```
Form.peopleA.currentCollection
```

* カレントの項目

```
Form.peopleA.currentItem
```

* カレント項目位置

```
Form.peopleA.currentItemPos
```

* 選択された項目

```
Form.peopleB.currentCollection
```

#### リストボックスのプロパティ（２個目）

 * コレクションまたはエンティティセレクション

```
Form.peopleB.currentCollection
```

* カレントの項目

```
Form.peopleB.currentItem
```

* カレント項目位置

```
Form.peopleB.currentItemPos
```

* 選択された項目

```
Form.peopleB.currentHighlight
```

* フォーム変数（``Form``）

```
// dialog_01_form

C_OBJECT($0;$form)

$form:=New object("people";New object;"peopleA";New object;"peopleB";New object)

$form.people.all:=ds.People.all().orderBy("lastNameYomi")

$form.peopleA.currentCollection:=$form.people.all
$form.peopleA.currentItem:=Null
$form.peopleA.currentItemPos:=Null
$form.peopleA.currentHighlight:=Null

$form.peopleB.currentCollection:=ds.People.newSelection(dk keep ordered)
$form.peopleB.currentItem:=Null
$form.peopleB.currentItemPos:=Null
$form.peopleB.currentHighlight:=Null

$0:=$form
```

* フォームイベント

```
$event:=Form event

Case of 
  : ($event=On Load)
    
    If (OB Is empty(Form))
      
      $form:=dialog_01_form 
      
      C_TEXT($name)
      For each ($name;$form)
        Form[$name]:=$form[$name]
      End for each 
      
    End if 
        
End case 
```

* １個目のカラム

```
This.lastName
```

あるいは

```
This.lastName+"（"+This.lastNameYomi+"）"
```

* ２個目のカラム

```
This.firstName
```

あるいは

```
This.firstName+"（"+This.firstNameYomi+"）"
```

* ダイアログの実行

```
$window:=Open form window("people")

$form:=dialog_01_form 

DIALOG("people";$form)
```

## フォームオブジェクト

* 変数

```
Form.peopleA.currentCollection.length
```

```
Form.people.all.length
```

* サーモメーター

```
(Form.peopleA.currentCollection.length/Form.people.all.length)*100
```

## クエリ（単）

* エンティティセレクションの作成（``entitySelection.query()`` ``entitySelection.orderBy()``）

```
//query_a_1

C_TEXT($1;$q)

$q:=$1

If ($q="")
  
  Form.peopleA.currentCollection:=Form.people.all
  
Else 
  
  Form.peopleA.currentCollection:=ds.People\
  .query("(firstName = :1 or lastName = :1 or firstNameYomi = :1 or lastNameYomi = :1)";Replace string($q;"@";"";*))\
  .orderBy("lastNameYomi")
  
End if 
```

* テキスト入力の処理（``Get edited text`` ``OBJECT Get name`` ``GOTO OBJECT``）

```
$event:=Form event

Case of 
  : ($event=On After Edit)
    
    If (Get edited text="")
      query_a_1 (Get edited text)
    End if 
    
  : ($event=On Data Change)
    
    query_a_1 (Form.query_a)
    
    GOTO OBJECT(*;OBJECT Get name(Object current))
    
End case 
```

* メタ情報

各リストボックスの「メタ情報」プロパティにフォーミュラを入力します。

```
query_a_meta (This;Form.peopleB.currentCollection)
```

メソッド（``query_a_meta``）は下記のコードでインポートできます。

```
C_TEXT($code)
$url:="https://raw.githubusercontent.com/4D-JP/learning-material/master/query_a_meta.txt"
If (HTTP Get($url;$code)=200)
  METHOD SET CODE("query_a_meta";$code)
  METHOD OPEN PATH("query_a_meta")
End if 
```

## クエリ（複）

* クエリプランとクエリパス

表示専用の「変数」オブジェクトをフォームに追加します。

```
Form.peopleA.queryPlan
```

```
Form.peopleA.queryPath
```
検索エリアのオブジェクトメソッドを更新します。

```
$event:=Form event

Case of 
  : ($event=On After Edit)
		
  	If (Get edited text="")
	  	query_a (Get edited text)
  	End if 
		
  : ($event=On Data Change)
		
  	query_a (Form.query_a)
		
  	GOTO OBJECT(*;OBJECT Get name(Object current))
		
End case 
```

メソッド（``query_a``）は下記のコードでインポートできます。

```
C_TEXT($code)
$url:="https://raw.githubusercontent.com/4D-JP/learning-material/master/query_a.txt"
If (HTTP Get($url;$code)=200)
  METHOD SET CODE("query_a";$code)
  METHOD OPEN PATH("query_a")
End if 
```

* ラジオボタングループ

「ラジオボタン」オブジェクトをフォームに追加してグループ化します。

```
Form.peopleA.and
```

```
Form.peopleA.or
```

``On Load``の処理は，フォームに記述します。

```
Form.peopleA.and:=1
Form.peopleA.or:=0
``` 

``On Clicked``の処理は，各ラジオボタンに記述します。
```
$event:=Form event

Case of 
  : ($event=On Clicked)
    
    query_a (\
    Choose(OBJECT Get name(Object with focus)="query_a";\
    Get edited text;Form.query_a))
    
End case 
```
