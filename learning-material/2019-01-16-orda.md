---
layout: page
title: 教材
permalink: /learning-material/2019-01-16/
standalone: true
---
    
* content
{:toc}

## スクリーンショット

<img width="500" alt="img1" src="https://user-images.githubusercontent.com/10509075/51163651-c5fc5c00-18dd-11e9-94ce-88cb5d147bd8.png">

<img width="500" alt="img2" src="https://user-images.githubusercontent.com/10509075/51164214-92bacc80-18df-11e9-8826-22763aed0df0.png">

## ダウンロード

* <i class="fa fa-download" aria-hidden="true"></i> [はじめる](https://github.com/4D-JP/learning-material/releases/download/2019-01-16/tutorial-start.zip)

* <i class="fa fa-download" aria-hidden="true"></i> [できた](https://github.com/4D-JP/learning-material/releases/download/2019-01-16/tutorial-finish.zip)

## データストアのセットアップ

* テキストファイルの読み込み（``Get 4D folder`` ``Document to text``）

* テーブルの作成（``IMPORT STRUCTURE``）

* データストアの更新（``RESTART 4D``）

* データストアオブジェクト（``ds``）

* コレクション型とオブジェクト型（``C_COLLECTION`` ``C_OBJECT``）

* エンティティの追加（``JSON Parse`` ``dataClass.new()`` ``entity.fromObject()`` ``entity.save()``）

**謝辞**: サンプルデータ by [tm-webtools.com](http://tm-webtools.com/Tools/TestData)

## フォーム

* リストボックス
* フォーム変数（``Form``）
* フォームイベント
* フォームメソッド
* ダイアログの実行（``DIALOG``）

「リストボックス」オブジェクトをフォームに追加してプロパティを設定します。

```
Form.peopleA.currentCollection
```

```
Form.peopleB.currentCollection
```

PeopleAのプロパティ

カレントの項目: ``Form.peopleA.currentItem``  

カレント項目位置: ``Form.peopleA.currentItemPos``  

選択された項目: ``Form.peopleB.currentCollection``

PeopleBのプロパティ

カレントの項目: ``Form.peopleB.currentItem``  

カレント項目位置: ``Form.peopleB.currentItemPos``  

選択された項目: ``Form.peopleB.currentHighlight``

PeopleBのエンティティセレクションは，PeopleAの選択された項目である点に注目してください。

１列目の式: ``This.lastName``または``This.lastName+"（"+This.lastNameYomi+"）"``　　

2列目の式: ``This.firstName``または``This.firstName+"（"+This.firstNameYomi+"）"``

フォーム変数をセットアップするメソッド（``dialog_01_form``）は下記のコードでインポートできます。

```
C_TEXT($code)
$url:="https://raw.githubusercontent.com/4D-JP/learning-material/master/dialog_01_form.txt"
If (HTTP Get($url;$code)=200)
  METHOD SET CODE("dialog_01_form";$code)
  METHOD OPEN PATH("dialog_01_form")
End if 
```

フォームをエディターから実行する場合，``Form``はデフォルトで空なので，フォームメソッドでプロパティを転写する必要があります。

```
$event:=Form event

Case of 
  : ($event=On Load)
    
    If (OB Is empty(Form))
      
      $form:=dialog_01_form 
      
      C_TEXT($name)
      For each ($name;$form)
        Form[$name]:=$form[$name]
      End for each 
      
    End if 
        
End case 
```

コマンドでダイアログを実行する場合，``Form``はパラメーターとして``DIALOG``に渡します。

```
$window:=Open form window("people")

$form:=dialog_01_form 

DIALOG("people";$form)
```

## クエリ（単）

* 変数オブジェクト
* オブジェクトメソッド
* メタ情報
* エンティティセレクションの作成（``entitySelection.query()`` ``entitySelection.orderBy()``）
* テキスト入力の処理（``Get edited text`` ``OBJECT Get name`` ``GOTO OBJECT``）
* サーモメーター

入力用の「変数」オブジェクトをフォームに追加してオブジェクトメソッドを記述します。

```
$event:=Form event

Case of 
  : ($event=On After Edit)
    
    If (Get edited text="")
      query_a_1 (Get edited text)
    End if 
    
  : ($event=On Data Change)
    
    query_a_1 (Form.query_a)
    
    GOTO OBJECT(*;OBJECT Get name(Object current))
    
End case 
```

メソッド（``query_a_1``）は下記のコードでインポートできます。

```
C_TEXT($code)
$url:="https://raw.githubusercontent.com/4D-JP/learning-material/master/query_a_1.txt"
If (HTTP Get($url;$code)=200)
  METHOD SET CODE("query_a_1";$code)
  METHOD OPEN PATH("query_a_1")
End if 
```

各リストボックスの「メタ情報」プロパティにフォーミュラを入力します。

```
query_a_meta (This;Form.peopleB.currentCollection)
```

メソッド（``query_a_meta``）は下記のコードでインポートできます。

```
C_TEXT($code)
$url:="https://raw.githubusercontent.com/4D-JP/learning-material/master/query_a_meta.txt"
If (HTTP Get($url;$code)=200)
  METHOD SET CODE("query_a_meta";$code)
  METHOD OPEN PATH("query_a_meta")
End if 
```

表示専用の「変数」オブジェクトおよび「サーモメーター」をフォームに追加します。

```
String(Form.peopleB.currentCollection.length)+" / "+String(Form.peopleA.currentCollection.length)
```

```
(Form.peopleB.currentCollection.length/Form.peopleA.currentCollection.length)*100
```

エンティティセレクションが変化すると，自動的に「サーモメーター」や「変数」がアップデートされる点に注目してください。

## クエリ（複）

* クエリプランとクエリパス
* ラジオボタングループ

表示専用の「変数」オブジェクトをフォームに追加します。

```
Form.peopleA.queryPlan
```

```
Form.peopleA.queryPath
```

検索エリアのオブジェクトメソッドを更新します。

```
$event:=Form event

Case of 
  : ($event=On After Edit)
		
  	If (Get edited text="")
	  	query_a (Get edited text)
  	End if 
		
  : ($event=On Data Change)
		
  	query_a (Form.query_a)
		
  	GOTO OBJECT(*;OBJECT Get name(Object current))
		
End case 
```

メソッド（``query_a``）は下記のコードでインポートできます。

```
C_TEXT($code)
$url:="https://raw.githubusercontent.com/4D-JP/learning-material/master/query_a_1.txt"
If (HTTP Get($url;$code)=200)
  METHOD SET CODE("query_a_1";$code)
  METHOD OPEN PATH("query_a_1")
End if 
```

「ラジオボタン」オブジェクトをフォームに追加してグループ化します。

```
Form.peopleA.and
```

```
Form.peopleA.or
```

``On Load``の処理は，フォームに記述します。

```
Form.peopleA.and:=1
Form.peopleA.or:=0
``` 

``On Clicked``の処理は，各ラジオボタンに記述します。
```
$event:=Form event

Case of 
  : ($event=On Clicked)
    
    query_a (\
    Choose(OBJECT Get name(Object with focus)="query_a";\
    Get edited text;Form.query_a))
    
End case 
```
