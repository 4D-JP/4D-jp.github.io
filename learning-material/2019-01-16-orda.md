---
layout: page
title: 教材
permalink: /learning-material/2019-01-16/
standalone: true
---
    
* content
{:toc}

## データストアのセットアップ

* テキストファイルの読み込み（``Get 4D folder`` ``Document to text``）

* テーブルの作成（``IMPORT STRUCTURE``）

* データストアの更新（``RESTART 4D``）

* データストアオブジェクト（``ds``）

* コレクション型とオブジェクト型（``C_COLLECTION`` ``C_OBJECT``）

* エンティティの追加（``JSON Parse`` ``dataClass.new()`` ``entity.fromObject()`` ``entity.save()``）

**謝辞**: サンプルデータ by [tm-webtools.com](http://tm-webtools.com/Tools/TestData)

## ユーザーインタフェース

#### リストボックスのプロパティ（１個目）

 * コレクションまたはエンティティセレクション

```
Form.peopleA.currentCollection
```

* カレントの項目

```
Form.peopleA.currentItem
```

* カレント項目位置

```
Form.peopleA.currentItemPos
```

* 選択された項目

```
Form.peopleB.currentCollection
```

#### リストボックスのプロパティ（２個目）

 * コレクションまたはエンティティセレクション

```
Form.peopleB.currentCollection
```

* カレントの項目

```
Form.peopleB.currentItem
```

* カレント項目位置

```
Form.peopleB.currentItemPos
```

* 選択された項目

```
Form.peopleB.currentHighlight
```

* フォーム変数（``Form``）

```
// dialog_01_form

C_OBJECT($0;$form)

$form:=New object("people";New object;"peopleA";New object;"peopleB";New object)

$form.people.all:=ds.People.all().orderBy("lastNameYomi")

$form.peopleA.currentCollection:=$form.people.all
$form.peopleA.currentItem:=Null
$form.peopleA.currentItemPos:=Null
$form.peopleA.currentHighlight:=Null

$form.peopleB.currentCollection:=ds.People.newSelection(dk keep ordered)
$form.peopleB.currentItem:=Null
$form.peopleB.currentItemPos:=Null
$form.peopleB.currentHighlight:=Null

$0:=$form
```

* フォームイベント

```
$event:=Form event

Case of 
	: ($event=On Load)
		
		If (OB Is empty(Form))
			
			$form:=dialog_01_form 
			
			C_TEXT($name)
			For each ($name;$form)
				Form[$name]:=$form[$name]
			End for each 
			
		End if 
				
End case 
```

* １個目のカラム

```
This.lastName
```

あるいは

```
This.lastName+"（"+This.lastNameYomi+"）"
```

* ２個目のカラム

```
This.firstName
```

あるいは

```
This.firstName+"（"+This.firstNameYomi+"）"
```

* ダイアログの実行

```
$window:=Open form window("people")

$form:=dialog_01_form 

DIALOG("people";$form)
```

## フォームオブジェクト

* 変数

```
Form.peopleA.currentCollection.length
```

```
Form.people.all.length
```

* サーモメーター

```
(Form.peopleA.currentCollection.length/Form.people.all.length)*100
```

## クエリ（単）

* エンティティセレクションの作成（``entitySelection.query()`` ``entitySelection.orderBy()``）

```
//query_a_1

C_TEXT($1;$q)

$q:=$1

If ($q="")
	
	Form.peopleA.currentCollection:=Form.people.all
	
Else 
	
	Form.peopleA.currentCollection:=ds.People\
	.query("(firstName = :1 or lastName = :1 or firstNameYomi = :1 or lastNameYomi = :1)";Replace string($q;"@";"";*))\
	.orderBy("lastNameYomi")
	
End if 
```

* テキスト入力の処理（``Get edited text`` ``OBJECT Get name`` ``GOTO OBJECT``）

```
$event:=Form event

Case of 
	: ($event=On After Edit)
		
		If (Get edited text="")
			query_a_1 (Get edited text)
		End if 
		
	: ($event=On Data Change)
		
		query_a_1 (Form.query_a)
		
		GOTO OBJECT(*;OBJECT Get name(Object current))
		
End case 
```

* メタ情報

```
query_a_meta (This)
```

あるいは

```
query_a_meta (This;Form.peopleB.currentCollection)
```

* メタ情報メソッド

```
//query_a_meta
C_OBJECT($1;$this;$0;$meta)

$this:=$1

$meta:=New object
	
Case of 
	: ($this.gender="男")
		
		$meta.fill:="#EAF2F8"
		
	: ($this.gender="女")
			
		$meta.fill:="#F5EEF8"
			
End case 
	
$0:=$meta
```

あるいは

```
//query_a_meta
C_OBJECT($1;$this;$2;$selection;$0;$meta)

$this:=$1
$selection:=$2

$meta:=New object

If ($selection.contains($this))
	
	Case of 
		: ($this.gender="男")
			
			$meta.fill:="#A9CCE3"
			
		: ($this.gender="女")
			
			$meta.fill:="#D7BDE2"
			
	End case 
	
	
Else 
	
	Case of 
		: ($this.gender="男")
			
			$meta.fill:="#EAF2F8"
			
		: ($this.gender="女")
			
			$meta.fill:="#F5EEF8"
			
	End case 
	
End if 

$0:=$meta
```

## クエリ（複）

* クエリプランとクエリパス

```
Form.peopleA.queryPlan
```

```
Form.peopleA.queryPath
```

* ラジオボタングループ

```
$event:=Form event

Case of 
	: ($event=On Clicked)
		
		query_a (\
		Choose(OBJECT Get name(Object with focus)="query_a";\
		Get edited text;Form.query_a))
		
End case 
```

```
//query_a
C_TEXT($1;$q)

$q:=$1

If ($q="")
	
	Form.peopleA.currentCollection:=Form.people.all
	Form.peopleA.queryPlan:=""
	Form.peopleA.queryPath:=""
	
Else 
	
	C_COLLECTION($words)
	$words:=Split string(Replace string($q;"@";"";*);" ";sk diacritical | sk trim spaces | sk diacritical)
	
	C_OBJECT($querySettings)
	$querySettings:=New object("queryPlan";True;"queryPath";True;"parameters";New collection)
	
	$i:=1
	C_TEXT($operator)
	$operator:=Choose(Form.peopleA.and=1;" and ";" or ")
	$searchCriteriaTemplate:="(firstName = :n or lastName = :n or firstNameYomi = :n or lastNameYomi = :n)"
	$searchCriteria:=""
	
	C_TEXT($word)
	For each ($word;$words)
		$searchCriteria:=$searchCriteria+Choose($i=1;"";$operator)+Replace string($searchCriteriaTemplate;":n";":"+String($i);*)
		$querySettings.parameters.push("@"+$word+"@")
		$i:=$i+1
	End for each 
	
	C_OBJECT($currentCollection)
	$currentCollection:=ds.People\
	.query($searchCriteria;$querySettings)
	
	Form.peopleA.queryPlan:=$currentCollection.queryPlan
	Form.peopleA.queryPath:=$currentCollection.queryPath
	
	Form.peopleA.currentCollection:=\
	$currentCollection.orderBy("lastNameYomi")
	
End if 
```
