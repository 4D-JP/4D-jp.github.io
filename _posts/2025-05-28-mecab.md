---
layout: fix
title: "MeCab (English Version)"
date: 2025-06-08 08:00:00
categories: 仕様
tags: programming language unicode
---

### Deprecation of MeCab for Japanese text processing

Previous versions of 4D could use the open-source [MeCab](https://developer.4d.com/docs/settings/database#support-of-mecab-japanese-version) library to generate a list of keyword-like tokens from Japanese text. Starting with **20 R9**, this feature is **deprecated**. We recommended developers to start planning for **20 R10** when MeCab will be **removed**. 

> [!TIP]
> Text in the Japanese language can not simply be broken into words. There are no spaces or boundaries that define the beginning and end of a token. MeCab is a widely used text analyzer designed specifically to process Japanese text.

> [!INFO]
> As of 20 R9, MeCab is installed with 4D only if the Japanese version is selected during installation on Windows. On macOS, MeCab is always included. Removal of the library [reduces the size of the application by about 80 MB](https://blog.4d.com/reduce-your-4d-apps-size-with-these-new-features/).

MeCab is used when the following conditions are met:

* "[Current data language](https://developer.4d.com/docs/settings/database#support-of-mecab-japanese-version)" is set to "Japanese"  
* "[Consider only non-alphanumeric chars for keywords](https://developer.4d.com/docs/settings/database#support-of-mecab-japanese-version)" is activated 
* the "mecab" folder and its contents is present inside the application's resources folder

Unless all 3 conditions are met, the ICU library is used for operations that work on keywords, such as:

* Keywords index set on fields of text or alpha type
* The [keywords](https://developer.4d.com/docs/Concepts/string#keywords) (`%`) operator for query and string comparison
* [`GET TEXT KEYWORDS`](https://developer.4d.com/docs/commands/get-text-keywords)
* [`DISTINCT VALUES`](https://developer.4d.com/docs/commands/distinct-values) on on fields of text or alpha type with keywords index set

> [!INFO]
> [`GET PICTURE KEYWORDS`](https://developer.4d.com/docs/commands/get-picture-keywords) does not use MeCab.

The following tables show how the same Japanese source text is broken into tokens based on which algorithm is used:

|Algorithm|Keywords|
|:-:|:-:|
|MeCab|和,布,蕪,使用,しています|
|Consider only non-alphanumeric chars for keywords|和布蕪を使用していますか|
|ICU|和布,蕪,を,使用,し,てい,ます,か|

#### How to plan for the removal of MeCab

First, confirm whether the application uses MeCab or not. If your **data language is not Japanese** or if your **database has no keywords index**, MeCab is **not used for indexing** (it may still be used for string operations).

```4d
If (Get database localization(Internal 4D localization)#"ja")
	return   //data language is not Japanese; not using mecab
End if 

$keywordsIndexedFields:=[]
For each ($dataClassName; ds)
	$dataClass:=ds[$dataClassName]
	For each ($attributeName; $dataClass)
		$attribute:=$dataClass[$attributeName]
		If ($attribute.kind="storage") && ($attribute.keywordIndexed)
			$keywordsIndexedFields.push($attribute)
		End if 
	End for each 
End for each 
```

Next, if your query uses keyword indexes (the `%` operator), make sure the search does not depend on any specific algorithm. For example, the following code assumes the input string qualifies as a single keyword which may or may not be true:

```4d
$source:="しています"
$selection:=ds.Table_1.query("Field_2 % :1"; $source)
//OK with MeCab, NG with ICU
```

You can future-proof the search by preprocessing the criteria using the same algorithm as the database engine:

```4d
$source:="和布蕪を使用していますか"
ARRAY TEXT($array; 0)
GET TEXT KEYWORDS($source; $array)
$keywords:=[]
ARRAY TO COLLECTION($keywords; $array)

$queryParams:={parameters: {}; arguments: {}}
$criteria:=[]
For each ($keyword; $keywords)
	$queryParams.parameters[$keyword]:=$keyword
	$criteria.push("Field_2 % :"+$keyword)
End for each 
$selection:=ds.Table_1.query($criteria.join(" and "); $queryParams)
//always OK
```

If you expect to switch between versions 20 R8 and 20 R10 during development, avoid opening large data files with significant number of fields that are indexed for keywords. 

Finally, schedule the upgrade to 20 R10 in deployment to give plenty of time for the reindexing of keywords. 